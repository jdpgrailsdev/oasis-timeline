import java.util.Date

plugins {
  id 'com.moowork.node' version '1.2.0'
  id 'org.ajoberstar.git-publish' version '0.3.2'
}

def outputDir = "$buildDir/dist"

gitPublish {
    repoUri = 'https://github.com/jdpgrailsdev/oasis-timeline.git'
    branch = 'gh-pages'

    contents {
        from(file(outputDir)) {
            into '.'
        }
    }

    commitMessage = 'Updating site.'
}

task start(type: YarnTask) {
    args = ['start']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task build(type: YarnTask, dependsOn:['yarn']) {
    args = ['build']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task clean(type: Delete) {
    delete "${projectDir}/package-lock.json"
    delete buildDir
}

task moveArtifacts(dependsOn:['build']) {
    doLast {
        ant.move(todir: outputDir) {
            fileset(dir: buildDir) {
                include(name:'*.js')
                include(name:'*.json')
                include(name:'*.html')
            }
        }
        ant.move file: "${buildDir}/favicons", todir: outputDir
        ant.move file: "${buildDir}/images", todir: outputDir
        ant.move file: "${buildDir}/static", todir: outputDir
        ant.move file: "${buildDir}/stylesheets", todir: outputDir
    }
}

task publish(dependsOn: 'gitPublishPush')

project.tasks.publish.dependsOn([project.tasks.moveArtifacts])
project.tasks.gitPublishPush.outputs.upToDateWhen { false }