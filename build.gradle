import java.util.Date

plugins {
  id 'com.moowork.node' version '1.3.1'
  id 'org.ajoberstar.git-publish' version '2.1.3'
}

def outputDir = file(gitPublish.repoDir)

gitPublish {
    repoUri = 'git@github.com:jdpgrailsdev/oasis-timeline.git'
    branch = 'gh-pages'

    contents {
        from outputDir
    }

    commitMessage = 'Updating site.'

    preserve {
        include '**/*'
    }
}

task start(type: YarnTask) {
    args = ['start']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task build(type: YarnTask, dependsOn:['yarn']) {
    args = ['build']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task clean(type: Delete) {
    delete "${projectDir}/package-lock.json"
    delete buildDir
}

task moveArtifacts(dependsOn:['build']) {
    inputs.files buildDir
    outputs.dir outputDir
    doLast {
        ant.move(todir: outputDir) {
            fileset(dir: buildDir) {
                include(name:'*.js')
                include(name:'*.json')
                include(name:'*.html')
            }
        }
        ant.copy file: "${projectDir}/CNAME", todir: outputDir
        ant.move file: "${buildDir}/favicons", todir: outputDir
        ant.move file: "${buildDir}/images", todir: outputDir
        ant.move file: "${buildDir}/static", todir: outputDir
        ant.move file: "${buildDir}/stylesheets", todir: outputDir
    }
}

project.tasks.gitPublishReset.finalizedBy 'moveArtifacts'
project.tasks.gitPublishPush.outputs.upToDateWhen { false }