plugins {
  id 'com.moowork.node' version '1.2.0'
  id 'org.ajoberstar.git-publish' version '0.3.2'
}

gitPublish {
    repoUri = 'https://github.com/jdpgrailsdev/oasis-timeline.git'
    branch = 'gh-pages'

    contents {
        from ("${buildDir}/dist") {
            into '.'
        }
    }

    commitMessage = 'Updating site.'
}

task start(type: YarnTask) {
    args = ['start']
}

task build(type: YarnTask, dependsOn:['yarn']) {
    args = ['build']
}

task clean(type: Delete) {
    delete "${projectDir}/package-lock.json"
    delete buildDir
}

task moveArtifacts {
    doLast {
        ant.move(todir: "${buildDir}/dist") {
            fileset(dir: buildDir) {
                include(name:'*.js')
                include(name:'*.json')
                include(name:'*.html')
            }
        }
        ant.move file: "${buildDir}/favicons", todir: "${buildDir}/dist/favicons"
        ant.move file: "${buildDir}/static", todir: "${buildDir}/dist/static"
    }
}

task publish(dependsOn: 'gitPublishPush')

project.tasks.publish.dependsOn([project.tasks.build])