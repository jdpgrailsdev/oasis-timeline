import com.moowork.gradle.node.npm.NpmTask
import java.util.Date

plugins {
  id 'com.github.node-gradle.node' version "${nodePluginVersion}"
  id 'org.ajoberstar.git-publish' version "${gitPublishPluginVersion}"
}

gitPublish {
    repoUri = 'https://github.com/jdpgrailsdev/oasis-timeline.git'
    branch = 'gh-pages'

    contents {
        from(projectDir) {
            include 'CNAME'
        }
        from(buildDir) {
            include '**.js'
            include '**.json'
            include '**.html'
            exclude 'dist/*.tar'
            exclude '__test__/**'
        }
        from("${buildDir}/favicons") {
            into 'favicons'
        }
        from("${buildDir}/fonts") {
            into 'fonts'
        }
        from("${buildDir}/images") {
            into 'images'
        }
        from("${buildDir}/sources") {
            into 'sources'
        }
        from("${buildDir}/static") {
            into 'static'
        }
        from("${buildDir}/stylesheets") {
            into 'stylesheets'
        }
    }

    commitMessage = 'Updating site.'
}

node {
    version = project['node.version']
}

task install(type: NpmTask) {
    npmCommand = ['install']
}

task start(type: NpmTask) {
    npmCommand = ['run-script']
    args = ['start']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task build(type: NpmTask, dependsOn:['clean', 'install']) {
    npmCommand = ['run-script']
    args = ['build']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task test(type: NpmTask, dependsOn:['clean', 'install']) {
    npmCommand = ['run-script']
    args = ['test', '--', '--watchAll=false']
}

task clean(type: Delete) {
    delete buildDir
}

task archive(type: Tar, dependsOn:['build']) {
    archiveBaseName = project.name
    destinationDirectory = file("${buildDir}/dist")
    from ("$buildDir") {
        exclude 'dist/*.tar'
        exclude '__test__/**'
    }
}

task check(dependsOn:[test]) {}

project.tasks.build.finalizedBy([project.tasks.archive])
project.tasks.build.dependsOn([project.tasks.test])
project.tasks.publish.dependsOn([project.tasks.build, project.tasks.gitPublishPush])
project.tasks.gitPublishPush.outputs.upToDateWhen { false }
