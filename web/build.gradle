import java.util.Date

plugins {
  id 'com.moowork.node' version '1.3.1'
  id 'org.ajoberstar.git-publish' version '3.0.0-rc.1'
}

gitPublish {
    repoUri = 'git@github.com:jdpgrailsdev/oasis-timeline.git'
    branch = 'gh-pages'

    contents {
        from(projectDir) {
            include 'CNAME'
        }
        from(buildDir) {
            include '**.js'
            include '**.json'
            include '**.html'
        }
        from("${buildDir}/favicons") {
            into 'favicons'
        }
        from("${buildDir}/images") {
            into 'images'
        }
        from("${buildDir}/static") {
            into 'static'
        }
        from("${buildDir}/stylesheets") {
            into 'stylesheets'
        }
    }

    commitMessage = 'Updating site.'
}

node {
    version = '14.1.0'
    yarnVersion = '1.22.4'
}

task start(type: YarnTask) {
    args = ['start']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task build(type: YarnTask, dependsOn:['clean', 'yarn']) {
    args = ['build']
    environment = [REACT_APP_UPDATED_AT: new Date().format('EEEEE, MMMM dd, yyyy')]
}

task clean(type: Delete) {
    delete "${projectDir}/package-lock.json"
    delete buildDir
}

project.tasks.publish.dependsOn([project.tasks.build, project.tasks.gitPublishPush])
project.tasks.gitPublishPush.outputs.upToDateWhen { false }